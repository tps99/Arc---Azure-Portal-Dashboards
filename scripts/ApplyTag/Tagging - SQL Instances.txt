//Tagging - SQL Instances
//Lists all SQL Instances for the purposes of Tagging
//1st version of Tagging script operates at Subscription Level
resources
| extend SQLVersion = properties.version
| extend SQLEdition = properties.edition
| extend containerId = tolower(tostring (properties.containerResourceId))
| where isnotempty(containerId)
| join kind=inner (
    resources
    | where type == "microsoft.hybridcompute/machines"
    | extend machineId = tolower(tostring(id))
    | project machineId, HostingVM = name, resourceGroup
)
on $left.containerId == $right.machineId
// Subscription Name
| join kind=inner (
    resourcecontainers
    | where type == 'microsoft.resources/subscriptions'
    | project subscriptionId, subscriptionName = name)
    on subscriptionId
| project resourceGroup, name, type = "SQLInstance",  
//Identifying attributes, ignored by Tagging Script
subscriptionName, SQLVersion, SQLEdition, SQLTags = ['tags'],
 //Tagging columns - add as needed, prefix with "TAG_" (this is removed by script such that "TAG_Application" creates a Tag called "Application")
 TAG_Application = "", TAG_Location = "", TAG_CommissionStatus = ""
